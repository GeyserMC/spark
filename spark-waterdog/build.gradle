plugins {
    id 'com.gradleup.shadow' version '8.3.0'
}

tasks.withType(JavaCompile) {
    // override, compile targeting J11
    options.release = 11
}

dependencies {
    implementation project(':spark-common')
    compileOnly('dev.waterdog.waterdogpe:waterdog:1.2.3') {
        exclude group: 'com.nukkitx.protocol'
    }
}

repositories {
    maven { url 'https://repo.opencollab.dev/main/' }
    maven { url 'https://repo.waterdog.dev/releases/' }
    maven { url 'https://repo.waterdog.dev/snapshots/' }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-waterdog.jar"

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.kyori.option', 'me.lucko.spark.lib.adventure.option'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude '**/*.proto'
    exclude '**/*.proto.bin'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
